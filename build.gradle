plugins {
	id 'java-gradle-plugin'
	id 'groovy-gradle-plugin'
	id 'maven-publish'
	id 'com.gradle.plugin-publish' version '0.16.0'
	
	id 'eclipse'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation gradleApi()
	implementation localGroovy()
	
	// https://mvnrepository.com/artifact/com.amazonaws/aws-java-sdk-codeartifact
	implementation group: 'com.amazonaws', name: 'aws-java-sdk-codeartifact', version: '1.12.28'
	// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
	
	testImplementation platform('org.junit:junit-bom:5.7.2')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation 'org.assertj:assertj-core:3.11.1'
	testImplementation 'org.mockito:mockito-core:3.11.+'
	
}

group = 'com.iyher.gradle.plugin'
version = '0.1'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.charSet = "UTF-8"
javadoc.options.encoding = "UTF-8"

sourceCompatibility = 1.8
targetCompatibility = 1.8


// https://github.com/gradle/gradle/issues/16273
// https://docs.gradle.org/current/javadoc/org/gradle/plugin/devel/GradlePluginDevelopmentExtension.html
// plugin definition can using after "extractPluginRequests" task registered.
tasks.whenTaskAdded { task ->
	if(!'extractPluginRequests'.equals(task.name))return
	gradlePlugin.plugins {
		named('com.github.tac-yacht.codeartifact-repository') {
			displayName = 'CodeArtifact Plugin.'
			description = 'codeArtifact more useful in gradle\'s repositories block.'
		}
	}
}

pluginBundle {
	website = 'https://github.com/tac-yacht/codeartifact-repository-gradleplugin'
	vcsUrl  = 'https://github.com/tac-yacht/codeartifact-repository-gradleplugin'
	tags = ['aws', 'codeartifact']
}

eclipse {
	classpath {
		downloadJavadoc = true
		downloadSources = true
	}
}

test {
	useJUnitPlatform()
}
